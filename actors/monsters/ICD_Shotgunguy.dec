// Hard hitting motherf-cker.

ACTOR ICD_ShotgunGuy replaces ShotgunGuy
{
	Health 30
	Radius 20
	Height 56
	Mass 100
	Speed 8
	PainChance 170
	Monster
	+FLOORCLIP
	SeeSound "shotguy/sight"
	PainSound "shotguy/pain"
	DeathSound "shotguy/death"
	ActiveSound "shotguy/active"
	Obituary "$OB_SHOTGUY"
	DropItem "ICD_SawedOffPickup"
	States
	{
	Spawn:
		SPOS AA 5 A_Look
		Loop
	See:
		SPOS AABBCCDD 3 A_Chase
		Loop
	Missile:
		SPOS E 6 A_FaceTarget
		TNT1 A 0 A_PlaySound("weapons/sawedoff/fire", CHAN_WEAPON)
		TNT1 AAAAA 0 A_CustomMissile("ICD_Gun3_Sparks", 30,5,frandom(-5,5),CMF_OFFSETPITCH,frandom(-5,5))
		TNT1 AAAA 0 A_CustomMissile("ICD_SSGSmoke1", 30,5,frandom(-5,5),CMF_OFFSETPITCH,frandom(-5,5))
		SPOS F 8 Bright A_CustomBulletAttack(9,9,8,(4/2*random(1,3)),"BulletPuff",360)
		TNT1 A 0 A_PlaySound("weapons/sawedoff/cock", 5)
		SPOS E 9
		TNT1 A 0 A_Jump(220,"See")
		// Aww. He's reloading!
		SPOS E 9 A_PlaySound("weapons/generic/emptyclick2")
		SPOS E 12 A_PlaySound("weapons/doublebarrel/load")
		SPOS E 12 A_PlaySound("weapons/doublebarrel/load")
		SPOS E 12 A_PlaySound("weapons/doublebarrel/load")
		SPOS E 12 A_PlaySound("weapons/doublebarrel/load")
		SPOS A 6 A_PlaySound("weapons/sawedoff/cock", 5)
		Goto See
	Pain:
		SPOS G 3
		SPOS G 3 A_Pain
		Goto See
	Death:
		SPOS H 5
		SPOS I 5 A_Scream
		SPOS J 5 A_NoBlocking
		SPOS K 5
		SPOS L 500
		SPOS L 3 A_SpawnItem("ICD_Flies")
		SPOS L -1 A_SpawnItem("ICD_Flies")
		Stop
	XDeath:
		SPOS M 5
		SPOS N 5 A_XScream
		SPOS O 5 A_NoBlocking
		SPOS PQRST 5
		SPOS U 400
		SPOS U 3 A_SpawnItem("ICD_Flies")
		SPOS U -1 A_SpawnItem("ICD_Flies")
		Stop
	Raise:
		SPOS L 5
		SPOS KJIH 5
		Goto See
	}
}