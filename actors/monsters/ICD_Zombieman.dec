ACTOR ICD_ZombieMan replaces Zombieman
{
	Health 20
	Radius 20
	Height 56
	Speed 8
	PainChance 200
	Monster
	+FLOORCLIP
	SeeSound "grunt/sight"
	AttackSound "grunt/attack"
	PainSound "grunt/pain"
	DeathSound "grunt/death"
	ActiveSound "grunt/active"
	Obituary "$OB_ZOMBIE" // "%o was killed by a zombieman."
	DropItem "ICD_9mmClip"
	States
	{
	Spawn:
		POSS A 5 A_Look
		Loop
	See:
		POSS A 4 A_Chase
		POSS A 3 A_Chase
		POSS B 4 A_Chase
		POSS B 3 A_Chase
		POSS C 4 A_Chase
		POSS C 3 A_Chase
		POSS D 4 A_Chase
		POSS D 3 A_Chase
		Loop
	Missile:
		POSS E 10 A_FaceTarget
		TNT1 A 0 //A_PlaySound
		POSS F 3 Bright A_CustomBulletAttack(9,9,1,(4/2*random(1,4)),"ICD_BulletPuff",500)
		TNT1 A 0 A_CustomMissile("ICD_SSGSmoke1", 30,5,frandom(-5,5),CMF_OFFSETPITCH,frandom(-5,5))
		POSS F 2 Bright
		POSS E 5 A_Jump(253,"See")
	Missile.Reload:
		POSS E 9 A_PlaySound("weapons/generic/emptyclick2")
		POSS E 4 A_PlaySound("weapons/1911/clipout")
		POSS E 12	// UZI_CLIP_DROP1
		POSS E 12 A_PlaySound("weapons/1911/clipin")
		POSS E 6 A_PlaySound("weapons/uzi/suzi2")
		Goto See
	Pain:
		POSS G 3
		POSS G 3 A_Pain
		Goto See
	Death:
		POSS H 5
		POSS I 2 A_Scream
		POSS I 3
		POSS J 5 A_NoBlocking
		POSS K 5
		TNT1 A 0 A_DropItem("ICD_KnifeAmmo",-1,26)
		TNT1 A 0 A_Jump(38,2)
		POSS L -1
		Stop
		POSS L 500
		POSS L 3 A_SpawnItemEx("ICD_Flies")
		POSS L -1 A_SpawnItemEx("ICD_Flies")
		Stop
	XDeath:
		POSS M 5
		POSS N 5 A_XScream
		POSS O 5 A_NoBlocking
		POSS PQRST 5
		TNT1 A 0 A_Jump(51,2)
		POSS U -1
		Stop
		POSS U 500
		POSS U 3 A_SpawnItemEx("ICD_Flies")
		POSS U -1 A_SpawnItemEx("ICD_Flies")
		Stop
	Raise:
		POSS K 5
		POSS JIH 5
		Goto See
	}
}