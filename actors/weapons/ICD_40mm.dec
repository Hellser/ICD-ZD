//Grenade Launcher
ACTOR ICD_40mm : BaseWeapon
{
	Game Doom
	Tag "$TAG_40mm"
	Weapon.AmmoType1 "ICD_40mmMagazine"
	Weapon.AmmoUse1 1
	Weapon.AmmoGive1 0
	Weapon.AmmoType2 "ICD_RocketAmmo"
	Obituary "%o can now taste %p's ass from %k's 40mm."
	Weapon.UpSound "weapons/generic/weaponup"
	Tag "40mm Grenade Launcher"
	Inventory.PickupMessage "$Got40mm"
	+WEAPON.AMMO_OPTIONAL
	States
	{
		Select:
			GREN A 1 A_Raise
			Loop
		Deselect:
			GREN A 1 A_Lower
			Loop
		Ready:
			GREN A 1 A_WeaponReady(WRF_ALLOWRELOAD)
			Loop
		Fire:
			GREN A 0 A_JumpIfInventory("ICD_40mmMagazine",1,2)
			GREN A 0 A_JumpIfInventory("ICD_RocketAmmo",1,"Reload")
			Goto Ready
			GREN A 2 A_GunFlash
			GREN A 0 A_PlaySound("weapons/40mm/gren2",CHAN_WEAPON,1.0,0,ATTN_NORM)
			GREN A 2 A_FireCustomMissile("ICD_40mmGrenade",0,1,5,-2,0,4)
			GREN A 0 A_AlertMonsters
			GREN A 2
			GREN A 1
			GREN D 3
			GREN A 1
			GREN B 3 A_FireCustomMissile("ICD_40mm_Shell_Spawn", 0, 0, 5, -4)
			GREN C 3
			GREN A 5 A_Refire
			Goto Ready
		Reload:
			GREN A 1 A_WeaponReady
			TNT1 A 0 A_JumpIfInventory("ICD_40mmMagazine",5,"Ready")
			TNT1 A 0 A_JumpIfInventory("ICD_RocketAmmo", 1, "Reload.StateCheck")
			Goto Ready
		Reload.StateCheck:
			TNT1 A 0 A_JumpIfInventory("ICD_40mmMagazine", 4, "Reload.Pre5")
			TNT1 A 0 A_JumpIfInventory("ICD_40mmMagazine", 3, "Reload.Pre4")
			TNT1 A 0 A_JumpIfInventory("ICD_40mmMagazine", 2, "Reload.Pre3")
			TNT1 A 0 A_JumpIfInventory("ICD_40mmMagazine", 1, "Reload.Pre2")
			Goto Reload.Actual
		Reload.Pre5:
			GREN GH 4
			GREL A 4 A_PlaySound("weapons/40mm/open",CHAN_WEAPON,1.0,0,ATTN_NORM)
			Goto Reload.Actual5
		Reload.Pre4:
			GREN GH 4
			GREL A 4 A_PlaySound("weapons/40mm/open",CHAN_WEAPON,1.0,0,ATTN_NORM)
			Goto Reload.Actual4
		Reload.Pre3:
			GREN GH 4
			GREL A 4 A_PlaySound("weapons/40mm/open",CHAN_WEAPON,1.0,0,ATTN_NORM)
			Goto Reload.Actual3
		Reload.Pre2:
			GREN GH 4
			GREL A 4 A_PlaySound("weapons/40mm/open",CHAN_WEAPON,1.0,0,ATTN_NORM)
			Goto Reload.Actual2
		Reload.Actual:
			GREN A 5 A_PlaySound("weapons/generic/emptyclick2",CHAN_WEAPON,1.0,0,ATTN_NORM)
			GREN GH 4
		Reload.Actual1:
			GREL A 8 A_PlaySound("weapons/40mm/open",CHAN_WEAPON,1.0,0,ATTN_NORM)
			GREL J 2 A_PlaySound("weapons/40mm/load",CHAN_WEAPON,1.0,0,ATTN_NORM)
			TNT1 A 0 A_TakeInventory("ICD_RocketAmmo",1,TIF_NOTAKEINFINITE)
			TNT1 A 0 A_GiveInventory("ICD_40mmMagazine",1)
		Reload.Actual2:
			GREL AB 2
			GREL C 3
			GREL A 4
			GREL J 2 A_PlaySound("weapons/40mm/load",CHAN_WEAPON,1.0,0,ATTN_NORM)
			TNT1 A 0 A_JumpIfInventory("ICD_RocketAmmo", 1, 1)
			Goto Reload.Finish
			TNT1 A 0 A_TakeInventory("ICD_RocketAmmo",1,TIF_NOTAKEINFINITE)
			TNT1 A 0 A_GiveInventory("ICD_40mmMagazine",1)
			GREL A 2
		Reload.Actual3:
			GREL DE 3
			GREL A 4
			GREL J 2 A_PlaySound("weapons/40mm/load",CHAN_WEAPON,1.0,0,ATTN_NORM)
			TNT1 A 0 A_JumpIfInventory("ICD_RocketAmmo", 1, 1)
			Goto Reload.Finish
			TNT1 A 0 A_TakeInventory("ICD_RocketAmmo",1,TIF_NOTAKEINFINITE)
			TNT1 A 0 A_GiveInventory("ICD_40mmMagazine",1)
			GREL A 2
		Reload.Actual4:
			GREL FG 3
			GREL A 4
			GREL J 2 A_PlaySound("weapons/40mm/load",CHAN_WEAPON,1.0,0,ATTN_NORM)
			TNT1 A 0 A_JumpIfInventory("ICD_RocketAmmo", 1, 1)
			Goto Reload.Finish
			TNT1 A 0 A_TakeInventory("ICD_RocketAmmo",1,TIF_NOTAKEINFINITE)
			TNT1 A 0 A_GiveInventory("ICD_40mmMagazine",1)
			GREL A 2
		Reload.Actual5:
			GREL HI 3
			GREL A 4
			GREL J 2 A_PlaySound("weapons/40mm/load",CHAN_WEAPON,1.0,0,ATTN_NORM)
			TNT1 A 0 A_JumpIfInventory("ICD_RocketAmmo", 1, 1)
			Goto Reload.Finish
			TNT1 A 0 A_TakeInventory("ICD_RocketAmmo",1,TIF_NOTAKEINFINITE)
			TNT1 A 0 A_GiveInventory("ICD_40mmMagazine",1)
		Reload.Finish:
			GREL A 10
			GREN H 3 A_PlaySound("weapons/40mm/close",CHAN_WEAPON,1.0,0,ATTN_NORM)
			GREN G 3
			GREN A 2
			Goto Ready
		Altfire:
			GREN A 1
			GREN GH 3
			GREL A 6 A_PlaySound("weapons/40mm/open")
			GREL J 2 A_PlaySound("weapons/40mm/load")
			GREL A 2
			GREL HIA 3
			GREN H 3 A_PlaySound("weapons/40mm/close")
			GREN G 3
			GREN A 4
			GREN A 2 A_FireCustomMissile("ICD_40mmSmokenade",0,0,5,-2,0,4)
			GREN A 0 A_AlertMonsters
			GREN A 0 A_GunFlash
			GREN A 0 A_PlaySound("weapons/40mm/fire",CHAN_WEAPON,1.0,0,ATTN_NORM)
			GREN A 2 ACS_NamedExecute("ICD_Recoil",0,6,8,300)
			GREN A 2
			GREN D 3
			GREN A 1
			GREN BC 3
			GREN A 5 A_Refire
			Goto Ready
		Flash:
		Altflash:
			GRNF A 2 BRIGHT A_Light1
			GRNF B 2 A_Light2
			GRNF C 2 A_Light1
			MISF B 0 A_Light0
			Stop

	}
}

ACTOR ICD_40mmMushroom
{
	Radius 8
	Height 8
	Speed 8
	Damage 5
	ReactionTime 75
	BounceType "Doom"
	BounceSound "weapon/40mm/bounce"
	Scale 0.5
	PROJECTILE
	-NOGRAVITY
	BounceType Doom
	BounceFactor 0.6
	States
	{
		Spawn:
			SMPF FGHI 2 BRIGHT A_SpawnItem("ICD_Expl_Puff")
			Loop
		Death:
			TNT1 A 0 A_Explode(60)
			SMPF ABCDE 4 BRIGHT A_FadeOut
			Stop
	}
}

ACTOR ICD_Expl_Puff : ICD_GunSmokeBase
{
	Radius 2
	Height 2
	Alpha 0.7
	Scale 0.2
	States
	{
		Spawn:
			BFE1 GHIKMOQSUW 2 BRIGHT
			Stop
	}
}

ACTOR ICD_40mmMagazine : Ammo
{
	Inventory.MaxAmount 5
	Ammo.BackpackAmount 0
	Ammo.BackpackMaxAmount 5
	Inventory.Icon SHOTA0
	+INVENTORY.IGNORESKILL
}

ACTOR ICD_40mmGrenade
{
	Radius 8
	Height 8
	Speed 30
	Damage 5
	ReactionTime 75
	//BounceSound "weapons/40mm/bounce"
	Scale 0.8
	PROJECTILE
	-NOGRAVITY
	+USEBOUNCESTATE
	+BOUNCEONACTORS
	BounceType Doom
	BounceFactor 0.5
	var int user_40mmgrenadetic;
	States
	{
		Spawn:
			GRNL A 1 A_SpawnItemEx("ICD_RPUFF",0,0,-6)
			TNT1 A 0 A_SetUserVar("user_40mmgrenadetic",user_40mmgrenadetic+1)
			TNT1 A 0 A_JumpIf(user_40mmgrenadetic>75,"True.Death")
			Loop
		Bounce:
			GRNL A 1 A_PlaySound("weapons/40mm/bounce")
			TNT1 A 0 A_SpawnItemEx("ICD_RPUFF",0,0,-6)
			TNT1 A 0 A_SetUserVar("user_40mmgrenadetic",user_40mmgrenadetic+1)
			TNT1 A 0 A_JumpIf(user_40mmgrenadetic>75,"True.Death")
			Goto Spawn
		Bounce.Actor:
			Goto True.Death
		Death:
			GRNL A 1 A_ChangeFlag("SOLID", 0)
			TNT1 A 0 A_SetUserVar("user_40mmgrenadetic",user_40mmgrenadetic+1)
			TNT1 A 0 A_JumpIf(user_40mmgrenadetic>75,"True.Death")
			Loop
		True.Death:
			TNT1 A 0 A_SpawnItemEx("ICD_40mmExplosion",0,0,-10,0,0,0,0,SXF_TRANSFERPOINTERS|SXF_NOCHECKPOSITION)
			Stop

	}
}

ACTOR ICD_40mmExplosion
{
	+NOINTERACTION
	States
	{
		Spawn:
			// An ever knowing glitch in ICD, dropping explosions.
			// This is an updated port. So some things are better.
			TNT1 A 0 NoDelay A_PlaySound("weapons/boom")
			BFE1 G 2
			BFE1 H 2 BRIGHT A_Explode(150,64)
			TNT1 A 0 A_Mushroom("ICD_40mmMushroom", 4, 0, frandom(4,8),1.5)
			TNT1 AAAA 0 //A_Mushroom("ICD_40mmMushroom", 4, 0, frandom(4,6), frandom(0.7,2))
			BFE1 IJKLM 2 BRIGHT
			BFE1 N 2 BRIGHT A_SpawnItemEx("ICD_Explosion_Smoke")
			BFE1 OPQRSTUVW 2 BRIGHT
			Stop
	}
}

ACTOR ICD_40mmSmokenade
{
	Radius 8
	Height 8
	Speed 30
	Damage 20
	ReactionTime 75
	BounceType "Doom"
	BounceSound "weapons/40mm/gasbounce"
	Scale 0.8
	PROJECTILE
	-NOGRAVITY
	+BOUNCEONACTORS
	BounceType Doom
	BounceFactor 0.5
	var int user_gasgrenadelife;
	var int user_gasgrenadetic;
	States
	{
		Spawn:
			GASL A 2 A_SpawnItemEx("ICD_FlakSmoke",0,0,-4)
			TNT1 A 0 A_SetUserVar("user_gasgrenadetic",user_gasgrenadetic+1)
			TNT1 A 0 A_JumpIf(user_gasgrenadetic>43,"Death")
			Loop
		Death:
			GASL A 1
			TNT1 A 0 A_SetUserVar("user_gasgrenadetic",user_gasgrenadetic+1)
			TNT1 A 0 A_JumpIf(user_gasgrenadetic>43,"GasPop")
			// Sorry, not today!
			Loop
		GasPop:
			GASL A 2 A_PlaySound("weapons/40mm/gasopen")
		Gas:
			GASL A 2 A_Stop
			GASL A 2 A_CustomMissile("ICD_GAS3_Spawn",0,0,random(-4,4),CMF_AIMDIRECTION | CMF_OFFSETPITCH, random(8,32))
			GASL A 2 A_SetAngle(Angle+30)
			TNT1 A 0 A_SetUserVar("user_gasgrenadelife",user_gasgrenadelife+1)
			TNT1 A 0 A_JumpIf(user_gasgrenadelife>100,"True.Death")
			Loop
		True.Death:
			GASL A 100
			GASL AAAA 100 A_FadeOut
			Stop
	}
}

ACTOR ICD_GAS3_Spawn
{
	Radius 8
	Height 8
	Speed 2
	Damage 0
	BounceType "Doom"
	Scale 0.8
	Alpha 0.4
	Renderstyle translucent
	PROJECTILE
	+BOUNCEONACTORS
	BounceType Doom
	DamageType "Smoke"
	BounceFactor 0.8
	var int user_gasgrenadelife;
	var int user_gasgrenadetic;
	States
	{
		Spawn:
			TNT1 A 0 NoDelay A_PlaySound("weapons/40mm/gashiss")
			GAS1 A 2 A_ScaleVelocity(frandom(0.50,1))
			GAS1 B 3 A_Explode(20)
			GAS1 C 2
			GAS1 D 2
			GAS1 E 3 A_Explode(20)
			GAS1 F 3
			GAS1 G 3
			GAS1 H 3 A_Explode(20)
			GAS1 I 3
			GAS1 J 3
			GAS1 F 3 A_Explode(20)
			GAS1 G 4
			GAS1 H 4
			GAS1 I 4 A_Explode(20)
			GAS1 J 4
			GAS1 F 4
			GAS1 G 4 A_Explode(20)
			GAS1 H 4
			GAS1 I 4
			GAS1 J 4 A_Explode(20)
			GAS1 F 4
			GAS1 G 4
			GAS1 H 4 A_Explode(20)
			GAS1 I 4
			GAS1 J 4
			GAS1 F 4 A_Explode(20)
			Goto Death
		Death:
			GAS1 G 4
			GAS1 H 4
			GAS1 I 4
			GAS1 J 4 A_FadeOut
			GAS1 F 4 A_FadeOut
			GAS1 G 4 A_FadeOut
			GAS1 H 4 A_FadeOut
			Stop
	}
}