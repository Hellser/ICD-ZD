ACTOR ICD_AssaultRifle : BaseWeapon
{
	Game Doom
	Weapon.SelectionOrder 1900
	Weapon.AmmoUse 1
	Weapon.AmmoType "ICD_AssaultRifleMagazine"
	Weapon.AmmoGive2 12
	Weapon.AmmoType2 "ICD_RifleAmmo"
	Obituary "%o was filled with lead from %k's assault rifle!"
	+WEAPON.NOALERT
	+WEAPON.AMMO_OPTIONAL
	Weapon.UpSound "weapons/generic/weaponup"
	Tag "Assault Rifle"
	States
	{
		Select:
			CHGG A 1 A_Raise
			Loop
		Deselect:
			CHGG A 1 A_Lower
			Loop
		Ready:
			CHGG A 1 A_WeaponReady(WRF_ALLOWRELOAD)
			Loop
		Fire:
			TNT1 A 0 A_JumpIfInventory("ICD_AssaultRifleMagazine",1,2)
			TNT1 A 0 A_JumpIfInventory("ICD_RifleAmmo",1,"Reload.After")
			Goto Ready
		Fire.Actual:
			CHGG A 0 A_JumpIfInventory("ICD_AssaultRifleMagazine",1,1)
			Goto Reload.After
			TNT1 A 0 A_AlertMonsters
			TNT1 A 0 A_PlaySound("weapons/assaultrifle/fire")
			CHGG B 1 BRIGHT A_FireBullets(2,2,-1,7,"ICD_BulletPuff")
			TNT1 A 0 BRIGHT A_Jump(168,2)
			TNT1 A 0 BRIGHT A_FireCustomMissile("ICD_TRACER1", frandom(-2,2),0,4,2,0,frandom(-2,2))
			TNT1 A 0 BRIGHT A_FireCustomMissile("ICD_M16_Smoke", frandom(-5,5),0,4,2,0,frandom(-5,5))
			CHGG C 1 BRIGHT A_FireCustomMissile("ICD_AssaultCasingSpawner",0,0,14,-4)
			CHGG A 2
			CHGG A 0 A_JumpIfInventory("ICD_AssaultRifleMagazine",1,1)
			Goto Reload.After
			TNT1 A 0 A_AlertMonsters
			TNT1 A 0 A_PlaySound("weapons/assaultrifle/fire")
			CHGG B 1 BRIGHT A_FireBullets(2,2,-1,7,"ICD_BulletPuff")
			TNT1 A 0 BRIGHT A_FireCustomMissile("ICD_M16_Smoke", frandom(-5,5),0,4,2,0,frandom(-5,5))
			CHGG C 1 BRIGHT A_FireCustomMissile("ICD_AssaultCasingSpawner",0,0,14,-4)
			CHGG A 2
			CHGG A 0 A_JumpIfInventory("ICD_AssaultRifleMagazine",1,1)
			Goto Reload.After
			TNT1 A 0 A_AlertMonsters
			TNT1 A 0 A_PlaySound("weapons/assaultrifle/fire")
			CHGG B 1 BRIGHT A_FireBullets(2,2,-1,7,"ICD_BulletPuff")
			TNT1 A 0 BRIGHT A_FireCustomMissile("ICD_M16_Smoke", frandom(-5,5),0,4,2,0,frandom(-5,5))
			CHGG C 1 BRIGHT A_FireCustomMissile("ICD_AssaultCasingSpawner",0,0,14,-4)
			CHGG A 2
			CHGG D 8 A_ReFire("Fire.Actual")
			Goto Ready
		AltFire:
			TNT1 A 0 A_JumpIfInventory("ICD_AssaultGrenadeMag",1,2)
			TNT1 A 0 A_JumpIfInventory("ICD_RocketAmmo",1,"AltFire.Reload")
			Goto Ready
			TNT1 A 0 A_AlertMonsters
			CHGG A 0
			TNT1 A 0 A_TakeInventory("ICD_AssaultGrenadeMag",1)
			CHGG A 4 A_GunFlash("Alt.Flash")
			CHGG A 0 A_PlaySound("weapons/assaultrifle/grenfire")
			CHGG D 4 A_FireCustomMissile("ICD_AssaultGrenade",0,1,4,-4,0,4)
			CHGG D 4
			CHGG A 0
			CHGG A 4
			TNT1 A 0 A_JumpIfInventory("ICD_RocketAmmo",1,"AltFire.Reload")
			Goto Ready
		AltFire.Reload:
			CHGG A 3
			CHGG E 4
			CHGG F 4
			CHGG J 8 A_PlaySound("weapons/doublebarrel/close")
			TNT1 A 0
			CHGG J 12
			CHGG J 8 A_PlaySound("weapons/40mm/load")
			TNT1 A 0 A_TakeInventory("ICD_RocketAmmo",1)
			TNT1 A 0 A_GiveInventory("ICD_AssaultGrenadeMag",1)
			CHGG J 12
			CHGG J 12 A_PlaySound("weapons/doublebarrel/open")
			CHGG F 3
			CHGG E 3
			CHGG A 2
			Goto Ready
		Reload:
			CHGG A 1 A_WeaponReady
			TNT1 A 0 A_JumpIfInventory("ICD_AssaultRifleMagazine",45,"Ready")
			TNT1 A 0 A_JumpIfInventory("ICD_RifleAmmo", 1, "Reload.Actual")
			Goto Ready
		Reload.After:
			CHGG A 3 A_PlaySound("weapons/generic/emptyclick2")
			CHGG A 3 A_PlaySound("weapons/generic/emptyclick2")
			CHGG A 3 A_PlaySound("weapons/generic/emptyclick2")
			Goto Reload+2
		Reload.Actual:
			CHGG E 4
			CHGG N 5
			CHGG K 4 A_PlaySound("weapons/assaultrifle/clipdrop", CHAN_WEAPON)
			CHGG L 5 // EJECT(M16_CLIP_DROP)
			CHGG G 10
			CHGG H 4
			CHGG I 4 A_PlaySound("weapons/assaultrifle/clipin", CHAN_WEAPON)
		Reload.InfoPhase:
			TNT1 A 0 A_TakeInventory("ICD_RifleAmmo",1)
			TNT1 A 0 A_GiveInventory("ICD_AssaultRifleMagazine",1)
			TNT1 A 0 A_JumpIfInventory("ICD_AssaultRifleMagazine",45,2)
			TNT1 A 0 A_JumpIfInventory("ICD_RifleAmmo",1,"Reload.InfoPhase")
			CHGG N 5
			CHGG F 5
			CHGG E 3
			CHGG O 4
			CHGG P 4
			CHGG Q 4 A_PlaySound("weapons/uzi/suzi2", CHAN_WEAPON)
			CHGG R 4
			CHGG O 3
			CHGG E 3
			Goto Ready
		Flash:
			TNT1 A 3 A_Light1
			TNT1 A 3 A_Light2
			TNT1 A 0 A_Light0
			Stop
		Alt.Flash:
			M203 A 2 BRIGHT A_Light1
			M203 B 2 BRIGHT A_Light2
			M203 C 2 A_Light0
			Stop
	}
}

ACTOR ICD_AssaultGrenade
{
	Radius 8
	Height 8
	Speed 55
	Damage 15
	PROJECTILE
	-NOGRAVITY
	States
	{
		Spawn:
			GRNL A 1 A_SpawnItemEx("ICD_RPUFF",0,0,-6)
			Loop
		Death:
			TNT1 A 0 A_SpawnItemEx("ICD_AssaultExplosion",0,0,-10,0,0,0,0,SXF_TRANSFERPOINTERS|SXF_NOCHECKPOSITION)
			Stop
	}
}

ACTOR ICD_AssaultExplosion
{
	+NOINTERACTION
	States
	{
		Spawn:
			// An ever knowing glitch in ICD, dropping explosions.
			// This is an updated port. So some things are better.
			TNT1 A 0 NoDelay A_PlaySound("weapons/boom")
			BFE1 G 1
			BFE1 H 1 BRIGHT A_Explode(128)
			BFE1 IJKLM 1 BRIGHT
			BFE1 N 1 BRIGHT A_SpawnItemEx("ICD_Explosion_Smoke")
			BFE1 OPQRSTUVW 1 BRIGHT
			Stop
	}
}

ACTOR ICD_AssaultRifleMagazine : Ammo
{
	Inventory.MaxAmount 45
	Ammo.BackpackAmount 0
	Ammo.BackpackMaxAmount 45
	Inventory.Icon CLIPA0
	+INVENTORY.IGNORESKILL
}

ACTOR ICD_AssaultGrenadeMag : Ammo
{
	Inventory.MaxAmount 1
	Ammo.BackpackAmount 0
	Ammo.BackpackMaxAmount 1
	+INVENTORY.IGNORESKILL
}