ACTOR ICD_Revolver : BaseWeapon
{
	Game Doom
	Weapon.AmmoType "ICD_RevolverMagazine"
	Weapon.AmmoType2 "ICD_Bullets"
	Obituary "%o went as wild as a weasel on %k."
	Inventory.Pickupmessage "Yes, milord."
	+WEAPON.NOALERT
	+WEAPON.AMMO_OPTIONAL
	Tag ".357 Revolver"
	States
	{
		Select:
			TNT1 A 0 A_PlaySound("weapons/generic/holsterup", CHAN_BODY)
			REVG A 1 A_Raise
			REVG A 5 A_Raise
			REVG A 0 A_PlaySound("weapons/1911/hammer", CHAN_WEAPON)
			REVG A 1 A_Raise
			Wait
		Deselect:
			REVG A 1 A_Lower
			Wait
		Ready:
			REVG A 1 A_WeaponReady(WRF_ALLOWRELOAD)
			Loop
		Fire:
			TNT1 A 0 A_JumpIfInventory("ICD_RevolverMagazine",1,2)
			TNT1 A 0 A_JumpIfInventory("ICD_Bullets",1,"Reload.After")
			Goto Ready
			TNT1 A 0 A_AlertMonsters
			TNT1 A 0 A_TakeInventory("ICD_RevolverMagazine", 1)
			TNT1 A 0 A_PlaySound("weapons/revolver/fire", CHAN_WEAPON)
			TNT1 A 0 A_GunFlash
			REVG H 2 A_FireBullets(1.2,1.2,-1,(28*random(1,1.285)),"ICD_BulletPuff")
			TNT1 A 0 A_FireCustomMissile("ICD_Gun3_Sparks", frandom(-5,5),0,0,0,0,frandom(-5,5))
			TNT1 A 0 A_FireCustomMissile("ICD_SSGSmoke1", frandom(-9,9),0,0,0,0,frandom(-5,5))
			TNT1 A 0 A_FireCustomMissile("ICD_Gun3_Sparks", frandom(-5,5),0,0,0,0,frandom(-5,5))
			REVG H 2 A_FireCustomMissile("ICD_SSGSmoke1", frandom(-9,9),0,0,0,0,frandom(-5,5))
			REVG C 2
			TNT1 A 0 A_Jump(128,2)
			REVG D 3
			REVG D 4
			REVG I 3
			REVG J 2 A_ReFire
			REVG A 0
			REVG A 2
			Goto Ready
		Flash:
			REVG B 2 Bright A_Light2
			TNT1 A 3 A_Light1
			TNT1 A 0 A_Light0
			Stop
		Reload:
			REVG A 1 A_WeaponReady
			TNT1 A 0 A_JumpIfInventory("ICD_RevolverMagazine", 6, "Ready")
			TNT1 A 0 A_JumpIfInventory("ICD_Bullets", 1, "Reload.Actual")
		Reload.Actual:
			REVG E 3
			REVG F 3
			REVG G 3 A_PlaySound("weapons/revolver/mout", CHAN_BODY)
			TNT1 A 20
			RHND A 4
		Reload.InfoPhase:
			TNT1 A 0 A_TakeInventory("ICD_Bullets", 1)
			TNT1 A 0 A_GiveInventory("ICD_RevolverMagazine", 1)
			TNT1 A 0 A_JumpIfInventory("ICD_RevolverMagazine", 6, 2)
			TNT1 A 0 A_JumpIfInventory("ICD_Bullets", 1, "Reload.InfoPhase")
			RHND B 3 A_PlaySound("weapons/revolver/mload", CHAN_BODY)
			RHND C 2
			RHND B 3
			RHND D 3
			TNT1 A 10
			REVG G 3 A_PlaySound("weapons/revolver/min", CHAN_BODY)
			REVG F 3
			REVG E 3
			REVG I 3
			REVG J 3 A_PlaySound("weapons/1911/hammer", CHAN_BODY)
			Goto Ready
	}
}

ACTOR ICD_RevolverMagazine : Ammo
{
	Inventory.MaxAmount 6
	Ammo.BackpackAmount 0
	Ammo.BackpackMaxAmount 6
	Inventory.Icon CLP1C0
	+INVENTORY.IGNORESKILL
}
