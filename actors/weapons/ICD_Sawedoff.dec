ACTOR ICD_SawedOff : BaseWeapon
{
	Game Doom
	Weapon.AmmoUse 1
	Weapon.AmmoGive 0
	Weapon.AmmoGive2 3
	+WEAPON.AMMO_OPTIONAL
	+WEAPON.NOALERT
	Weapon.AmmoType 	"ICD_SawedOffMagazine"
	Weapon.AmmoType2	"ICD_Shells"
	Inventory.PickupMessage "$GotSawedOff"
	Tag "Sawed-off Shotgun"
	Weapon.UpSound "weapons/generic/weaponup"
	States
	{
		Ready:
			SAWD A 1 A_WeaponReady(WRF_ALLOWRELOAD)
			Loop
		Deselect:
			SAWD A 1 A_Lower
			Loop
		Select:
			SAWD A 1 A_Raise
			Loop
		Fire:
			TNT1 A 0 A_JumpIfInventory("ICD_SawedOffMagazine",1,2)
			TNT1 A 0 A_JumpIfInventory("ICD_Shells", 1, "Reload")
			Goto Ready
			TNT1 A 0 A_AlertMonsters
			TNT1 A 0 //A_TakeInventory("ICD_SawedOffMagazine", 1)
			TNT1 A 0 A_PlaySound("weapons/sawedoff/fire", CHAN_WEAPON)
			SAWD B 2 bright A_FireBullets(7,2,10,4,"ICD_ShotgunBulletPuff")
			TNT1 AAAA 0 A_FireCustomMissile("ICD_Gun3_Sparks", frandom(-5,5),0,0,0,0,frandom(-5,5))
			TNT1 AAAA 0 A_FireCustomMissile("ICD_SSGSmoke1", frandom(-9,9),0,0,0,0,frandom(-5,5))
			SAWD C 2 bright
			SAWD E 2
			SAWD D 1
			SAWD A 2
			SAWD F 3
			SAWD G 4 A_PlaySound("weapons/sawedoff/cock", 5)
			SAWD H 4 A_FireCustomMissile("ICD_ShotgunShellSpawner1", 0, 0, -14, -4)
			SAWD G 5
			SAWD F 4
			SAWD A 2
			SAWD A 2 A_ReFire
			Goto Ready
		AltFire:
			SAWD A 1
			SAWD F 6
			SAWD M 8 A_PlaySound("weapons/flare/flarerun", 5, 1, 1)
			TNT1 A 0 A_GunFlash("AltFlash")
			SAWD M 2 A_PlaySound("weapons/flare/flaresoftignite", 6)
			SAWD M 4 A_FireCustomMissile("ICD_ThrownFlare", 0, 0)
			TNT1 A 0 A_StopSound(5)
			SAWD M 2
			SAWD M 12
			SAWD F 4
			Goto Ready
		AltFlash:
			BFGG D 2
			BFGG E 2
			BFGG F 2
			Stop
		Flash:
			SAWD B 2 Bright A_Light1
			SAWD C 2 Bright A_Light2
			Goto LightDone
		Reload: // Huh. The first reload is always faster. The fuck.
			SAWD A 1 A_WeaponReady
			TNT1 A 0 A_JumpIfInventory("ICD_SawedOffMagazine",6,"Ready")
			TNT1 A 0 A_JumpIfInventory("ICD_Shells", 1, 1)
		Goto Ready
			TNT1 A 0 A_PlaySound("weapons/generic/emptyclick2")
			SAWD A 1
			SAWD A 3
			SAWD D 3
		Reload.Actual:
			SAWD F random(2,3) // Holding Gun Up
			SAWD I 3
			SAWD J 4
			TNT1 A 0 A_PlaySound("weapons/doublebarrel/load", 6)
			TNT1 A 0 A_TakeInventory("ICD_Shells",1,TIF_NOTAKEINFINITE)
			TNT1 A 0 A_GiveInventory("ICD_SawedOffMagazine",1)
			SAWD K 3 A_WeaponReady(WRF_NOBOB)
			TNT1 A 0 A_JumpIfInventory("ICD_SawedOffMagazine", 6, "Reload.Done")
			TNT1 A 0 A_JumpIfInventory("ICD_Shells", 1, "Reload.Actual")
		Reload.Done:
			SAWD F 4
			SAWD G 4
			SAWD H 4 A_PlaySound("weapons/sawedoff/cock", 5)
			SAWD G 4
			SAWD F 3
			SAWD A 1
			Goto Ready
		Spawn:
			SAWD L -1
			Stop
	}
}

ACTOR ICD_SawedOffMagazine : Ammo
{
	Inventory.MaxAmount 6
	Ammo.BackpackAmount 0
	Ammo.BackpackMaxAmount 6
	Inventory.Icon SHELA0
	+INVENTORY.IGNORESKILL
}
