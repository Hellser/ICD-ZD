ACTOR ICD_DualPistol : BaseWeapon
{
	Game Doom
	Weapon.SelectionOrder 1900
	Weapon.AmmoUse 0
	Weapon.AmmoType "ICD_DualPistolMagazine"
	Weapon.AmmoGive2 12
	Weapon.AmmoType2 "ICD_Bullets"
	Obituary "%o was in the way of %k's Dualies!"
	Inventory.Pickupmessage "$PICKUP_PISGOL_DROPPED"
	Tag "Dual M1911"
	+WEAPON.NOALERT
	+WEAPON.AMMO_OPTIONAL
	States
	{
		Ready:
			PISG A 1 A_WeaponReady(WRF_ALLOWRELOAD)
			Loop
		Deselect:
			PISG A 1 A_Lower
			Loop
		Select:
			PISG A 0 A_PlaySound("weapons/generic/holsterup",5)
			PISG AAAAA 1 A_Raise
			PISG A 0 A_PlaySound("weapons/Dual1911/hammer",6)
			PISG A 1 A_Raise
			Goto Select+7
		Fire:
			// Reload Check Here
			TNT1 A 0 A_JumpIfInventory("ICD_DualPistolMagazine",1,2)
			TNT1 A 0 A_JumpIfInventory("ICD_Bullets",1,"Reload")
			Goto Ready
			TNT1 A 0 A_JumpIfInventory("ICD_DualPistolMechanic", 1, "Fire.Alternative")
			TNT1 A 0 A_GiveInventory("ICD_DualPistolMechanic", 1)
			TNT1 A 0 A_PlaySound("weapons/1911/fire", CHAN_WEAPON)
			TNT1 A 0 A_AlertMonsters
			TNT1 A 0 A_TakeInventory("ICD_DualPistolMagazine",1)
			PISG B 2 Bright A_FireBullets(1,1,1,5,"ICD_BulletPuff")
			TNT1 A 0 A_FireCustomMissile("ICD_PistolSmoke1", frandom(-5,5),0,3.5,2.4,0,frandom(-5,5))
			TNT1 A 0 A_FireCustomMissile("ICD_PistolCasingSpawner", 0, 0, 12, -4)
			PISG C 2
			TNT1 A 0 // Check for Reload.. again
			PISG A 0 A_Jump(127,"Ready")
			PISG A 2 A_ReFire
			Goto Ready
		Fire.Alternative:
			PISG A 0 // What's this?
			TNT1 A 0 A_TakeInventory("ICD_DualPistolMechanic", 1)
			TNT1 A 0 A_PlaySound("weapons/1911/fire", CHAN_WEAPON)
			TNT1 A 0 A_TakeInventory("ICD_DualPistolMagazine",1)
			PISG D 2 BRIGHT A_FireBullets(1,1,1,5,"ICD_BulletPuff")
			TNT1 A 0 A_AlertMonsters
			TNT1 A 0 A_FireCustomMissile("ICD_PistolSmoke1", frandom(-5,5),0,-3.5,2.4,0,frandom(-5,5))
			TNT1 A 0 A_FireCustomMissile("ICD_PistolCasingSpawner", 0, 0, -8, -4)
			PISG E 2
			PISG A 0 // Reload Check
			PISG A 1
			PISG A 0 A_ReFire
			Goto Ready
		AltFire:
			TNT1 A 0 A_JumpIfInventory("ICD_DualPistolMagazine",2,3)
			TNT1 A 0 A_JumpIfInventory("ICD_DualPistolMagazine",1,"Fire")
			TNT1 A 0 A_JumpIfInventory("ICD_Bullets",1,"Reload")
			Goto Ready
			TNT1 A 0 // Check Reload
			TNT1 A 0 A_PlaySound("weapons/1911/fire", CHAN_WEAPON)
			TNT1 A 0 A_PlaySound("weapons/1911/fire", 5)
			TNT1 A 0 A_AlertMonsters
			// Why did I do that? ICD-SE did it. It's to "Amplify" the sound. Crazy huh?
			DPST I 2 BRIGHT A_FireBullets(1,1,2,5,"ICD_BulletPuff")
			TNT1 A 0 A_TakeInventory("ICD_DualPistolMagazine",2)
			TNT1 A 0 A_FireCustomMissile("ICD_PistolSmoke1", frandom(-5,5),0,3.5,2.4,0,frandom(-5,5))
			TNT1 A 0 A_FireCustomMissile("ICD_PistolSmoke1", frandom(-5,5),0,-3.5,2.4,0,frandom(-5,5))
			TNT1 A 0 A_FireCustomMissile("ICD_PistolCasingSpawner", 0, 0, -8, -4)
			TNT1 A 0 A_FireCustomMissile("ICD_PistolCasingSpawner", 0, 0, 12, -4)
			TNT1 A 0 // Casing
			TNT1 A 0 // Casing
			DPIS G 2
			PISG A 6
			TNT1 A 0 A_ReFire
			PISG A 0 // Reload Check
			Goto Ready
		Reload:
			PISG A 1 A_WeaponReady
			TNT1 A 0 A_JumpIfInventory("ICD_DualPistolMagazine",24,"Ready")
			TNT1 A 0 A_JumpIfInventory("ICD_Bullets", 1, 1)
			Goto Ready
			TNT1 A 0 A_TakeInventory("ICD_DualPistolMechanic", 1)
			DPIS G 4
			DPIS A 4
			DPIS A 1 A_PlaySound("weapons/1911/clipout")
			TNT1 A 0 A_FireCustomMissile("ICD_EmptyPistolClipDrop",frandom(-6,6),0,random(1,3),-8,0,frandom(-3,3))
			TNT1 A 0 A_FireCustomMissile("ICD_EmptyPistolClipDrop",frandom(-6,6),0,random(-3,-1),-8,0,frandom(-3,3))
			DPIS A 6
			DPIS D 4 A_PlaySound("weapons/generic/holsterup",5)
			PIST F 14 A_PlaySound("weapons/1911/clipin")
			PISR A 2
			PISR B 3
			PISR F 3
			PISR A 2
			PISR F 2
			DPST A 2
			TNT1 B 14 A_PlaySound("weapons/generic/holsterup",5)
			DPST B 2
			DPST C 3
			DPST D 2 A_PlaySound("weapons/1911/clipin")
		Reload.InfoPhase:
			TNT1 A 0 A_TakeInventory("ICD_Bullets",1)
			TNT1 A 0 A_GiveInventory("ICD_DualPistolMagazine",1)
			TNT1 A 0 A_JumpIfInventory("ICD_DualPistolMagazine",24,2)
			TNT1 A 0 A_JumpIfInventory("ICD_Bullets",1,"Reload.Restart")
			DPST E 2
			DPST F 3
			DPST D 2
			DPST C 3
			DPST G 10 A_PlaySound("weapons/generic/holsterup",5)
			DPST H 3
			DPIS G 3
			PISG E 5 A_PlaySound("weapons/1911/slide")
			PISG A 2 A_PlaySound("weapons/1911/slide")
			PISG A 0 // I'm DONE!
			Goto Ready
		Reload.Restart:
			TNT1 A 0
			Goto Reload.InfoPhase
		Spawn:
			PISG A -1
			Stop
	}
}

ACTOR ICD_DualPistolMechanic : Inventory
{
	Inventory.MaxAmount 1
	+INVENTORY.IGNORESKILL
	-INVBAR
}

ACTOR ICD_DualPistolMagazine : Ammo
{
	Inventory.MaxAmount 24
	Ammo.BackpackAmount 0
	Ammo.BackpackMaxAmount 24
	Inventory.Icon CLIPB0
	+INVENTORY.IGNORESKILL
}
